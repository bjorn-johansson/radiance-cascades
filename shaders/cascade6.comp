
#version 430 core

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in; //TODO: explore better groups and compare gpu scheduling perf

//bitmap is 1 cell/uint. 2 least significant bits denote type:
//  00 = 0 = empty
//  01 = 1 = wall
//  10 = 2 = emissive
//  11 = 3 = transmissive - unused for now
//the remaining 6 denote material ID: 11111100
layout(binding = 0) uniform usampler2D _bitmapTexture;              //READ
layout(binding = 1) uniform sampler2D _materialAtlas;               //READ
layout(binding = 2, rgba32f) uniform writeonly image2DArray _cascadeTextures;   //WRITE

const float PI = 3.14159265359;

const float airAbsorption = float(0.05f);

const uint WALL_MASK = 0x1;
const uint EMITTER_MASK = 0x2;
const uint MATERIAL_MASK = 0xFC;

const float CASCADE_RAY_LENGTHS[5] = {4.0f, 7.0f, 12.0f, 20.0f, sqrt(2.0f) * 512.0f};

const int CASCADE_TEXTURE_WIDTH = 512;

const int CASCADE_LEVEL = 4; //this differs between cascade levels.

const int PROBE_CLUSTER_SIDE = int(pow(2, CASCADE_LEVEL + 1));
const int PROBE_COUNT = int(pow(CASCADE_TEXTURE_WIDTH / PROBE_CLUSTER_SIDE, 2));

const float RAY_LENGTH = CASCADE_RAY_LENGTHS[CASCADE_LEVEL];   // set to maximum possible length of a ray in the final cascade. based on bitmaps size.
const int RAY_COUNT = PROBE_CLUSTER_SIDE * PROBE_CLUSTER_SIDE;  //4x scaling, not folling penumbra condition(2x scaling), but easier to work with for now 
const int RAY_STEPS = int(floor(RAY_LENGTH * sqrt(2))) + 1;


//DDA raytracing
void main() {
    ivec2 id = ivec2(gl_GlobalInvocationID.xy);


    ivec2 bitmapSize = textureSize(_bitmapTexture, 0);
    vec2 bitmapScale = vec2(bitmapSize) / float(CASCADE_TEXTURE_WIDTH);
    //scale cascade to cover bitmap, place in center of pixel. This means grid isnt perfectly centered on screen, but removes issues of ro between pixels.
    vec2 po = vec2(id) * bitmapScale + 0.5f;

    uint ri = (id.y % PROBE_CLUSTER_SIDE) * PROBE_CLUSTER_SIDE + (id.x % PROBE_CLUSTER_SIDE);
    float angle = 2f * PI * ((float(ri) + 0.5f) / RAY_COUNT); //+0.5 to shift so half the rays are over 1pi radians
    vec2 rd = vec2(cos(angle), sin(angle));

    vec2 ro = po;
    for(int i = 0; i < CASCADE_LEVEL; i++){
        ro += rd * (CASCADE_RAY_LENGTHS[i] + 1);
    }
    ivec2 cell = ivec2(floor(ro));
    
    vec2 roFract = ro - cell;
    ivec2 step = ivec2(sign(rd));
    
    vec2 rayUnitStepSize = vec2(sqrt(1 + (rd.y / rd.x) * (rd.y / rd.x)),
                                sqrt(1 + (rd.x / rd.y) * (rd.x / rd.y)));
    
    // going in the direction of the ray, how far will we travel through each column/row?
    vec2 distToEdge;
    distToEdge.x = float(step.x >= 0.f) * (1 - roFract.x) * rayUnitStepSize.x;  //positive dir
    distToEdge.x += float(step.x < 0.f) * roFract.x * rayUnitStepSize.x;        //negative dir

    distToEdge.y = float(step.y >= 0.f) * (1 - roFract.y) * rayUnitStepSize.y;
    distToEdge.y += float(step.y < 0.f) * roFract.y * rayUnitStepSize.y;

    vec3 radiance = vec3(0.f);
    float prevDist = 0.f;
    float rayIsAlive = 1.0f;
    float gotBlocked = 0.f; //tells other cascades if this hit a wall
    float t = distance(po, ro);  //total distance traveled
    float rayMaxDistance = t + RAY_LENGTH;
    
    for(int i = 0; i < RAY_STEPS; i++) {
        //get object data of the current cell
        uint cellData = texelFetch(_bitmapTexture, cell, 0).r;// * uint(max(cell.x, cell.y) < min(bitmapSize.x, bitmapSize.y));

        //object bools
        float blocks = float((cellData & WALL_MASK) != 0u);
        float emits = float((cellData & EMITTER_MASK) != 0u);
        float transmits = 1.f - blocks;

        ////get the emissive material. If it isn't emissive set index to 0. should improve cache hits? TODO: try changing this and compare with tracy!
        //uint matID = (cellData >> 2) * uint(emits);
        //ivec2 matTexelCoord = ivec2(matID % 8, matID / 8);
        //vec4 material = texelFetch(_materialAtlas, matTexelCoord, 0); //0 vector at indx 0
        //vec3 emission = material.rgb * material.a;

        vec3 tempEmission = emits * vec3(1f,1f,1f);

        //gather radiance
        vec3 newRad = rayIsAlive * tempEmission * exp(-airAbsorption * (t / 25)); //beers law for attenuation
        newRad *= float(cell.x >= 0f) * float(cell.y >= 0f)  * float(cell.x < bitmapSize.x) * float(cell.y < bitmapSize.y);
        radiance += newRad;

        //a wall, a light or reaching max distance kills the ray.
        rayIsAlive *= (1.f - blocks) * (1.f - emits) * max(sign(rayMaxDistance - t), 0f);
        gotBlocked = max(gotBlocked, blocks);      
        
        //advance DDA
        int xCloser = int(abs(distToEdge.x) < abs(distToEdge.y));
        int yCloser = 1 - xCloser;
        
        float nextDist = mix(distToEdge.y, distToEdge.x, float(xCloser));
        
        t += abs(nextDist - prevDist);
        prevDist = nextDist;
        
        cell.x += xCloser * step.x;
        distToEdge.x += xCloser * rayUnitStepSize.x;
        cell.y += yCloser * step.y;
        distToEdge.y += yCloser * rayUnitStepSize.y;

    }
    //store collected ray data into cascade
    uvec2 rayCoordInProbe = id % PROBE_CLUSTER_SIDE;
    vec2 uvInProbe = vec2(rayCoordInProbe) / float(PROBE_CLUSTER_SIDE);
    imageStore(_cascadeTextures, ivec3(id, CASCADE_LEVEL), vec4(radiance, gotBlocked));

}
