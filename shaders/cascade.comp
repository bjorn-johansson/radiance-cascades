
#version 430 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in; //TODO: explore better groups and compare gpu scheduling perf

//bitmap is 1 cell/uint. 2 least significant bits denote type:
//  00 = 0 = empty
//  01 = 1 = wall
//  10 = 2 = emissive
//  11 = 3 = transmissive - unused for now
//the remaining 6 denote material ID: 11111100
layout(binding = 0) uniform usampler2D _bitmapTexture;              //READ
layout(binding = 1) uniform sampler2D _materialAtlas;               //READ
layout(binding = 2, rgba32f) uniform writeonly image2DArray _cascadeTextures;   //WRITE

uniform int _CascadeLevel; //this differs between cascade levels.

uniform int _RayLengthMultiplier = 1;

const float PI = 3.14159265359;

const float airAbsorption = float(0.05f);

const uint WALL_MASK = 0x1;
const uint EMITTER_MASK = 0x2;
const uint MATERIAL_MASK = 0xFC;

const int CASCADE_PROBE_SIDES[7] = { 2, 4, 8, 16, 32, 64, 128};


//const lengths instead of uniform to allow for ray loop unrolling, are these still const even if accessed via a uniform index?

const float SQRT2 = 1.41421356f;
const float CASCADE_RAY_LENGTHS[7] = {
    pow(4, 0) * SQRT2,
    pow(4, 1) * SQRT2,
    pow(4, 2) * SQRT2,
    pow(4, 3) * SQRT2,
    pow(4, 4) * SQRT2,
    pow(4, 5) * SQRT2,
    pow(4, 6) * SQRT2
};


const int CASCADE_TEXTURE_WIDTH = 1024;

const int PROBE_CLUSTER_SIDE = int(pow(2, _CascadeLevel + 1));
const int PROBE_COUNT = int(pow(CASCADE_TEXTURE_WIDTH / PROBE_CLUSTER_SIDE, 2));

const float RAY_LENGTH = CASCADE_RAY_LENGTHS[_CascadeLevel] * _RayLengthMultiplier;   // set to maximum possible length of a ray in the final cascade. based on bitmaps size.
const int RAY_COUNT = PROBE_CLUSTER_SIDE * PROBE_CLUSTER_SIDE;  //4x scaling, not folling penumbra condition(2x scaling), but easier to work with for now 
const int MAX_RAY_STEPS = int(floor(RAY_LENGTH * sqrt(2))) + 1;



//DDA raytracing
void main() {
    ivec2 id = ivec2(gl_GlobalInvocationID.xy);


    ivec2 bitmapSize = textureSize(_bitmapTexture, 0);
    vec2 bitmapScale = vec2(bitmapSize) / float(CASCADE_TEXTURE_WIDTH);
    
    //scale up probe coordinates to cover bitmap
    vec2 po = (vec2(id) - id % CASCADE_PROBE_SIDES[_CascadeLevel]); //top/bottom left in probe grid
    vec2 pc = po + CASCADE_PROBE_SIDES[_CascadeLevel] / 2f;  //center of probe in probe grid
    vec2 bitmapPC = pc * bitmapScale;

    uint ri = (id.y % PROBE_CLUSTER_SIDE) * PROBE_CLUSTER_SIDE + (id.x % PROBE_CLUSTER_SIDE);
    float angle = 2f * PI * ((float(ri) + 0.5f) / RAY_COUNT); //+0.5 to shift so half the rays are over 1pi radians
    vec2 rd = vec2(cos(angle), sin(angle));
    
    //shoot rays from probe centers.
    vec2 ro = bitmapPC;
    for(int i = 0; i < _CascadeLevel; i++){
        ro += rd * (CASCADE_RAY_LENGTHS[i] * _RayLengthMultiplier);
    }
    ivec2 cell = ivec2(floor(ro));
    
    vec2 roFract = ro - cell;
    ivec2 step = ivec2(sign(rd));
    
    vec2 rayUnitStepSize = vec2(sqrt(1 + (rd.y / rd.x) * (rd.y / rd.x)),
    sqrt(1 + (rd.x / rd.y) * (rd.x / rd.y)));
    // vec2(1.5f, 1.5f); //perfect diagonals have 1.5f distance through squares so this could just be assigned later
    
    // going in the direction of the ray, how far will we travel through each column/row?
    vec2 distToEdge; 
    distToEdge.x = float(step.x >= 0.f) * (1 - roFract.x) * rayUnitStepSize.x;  //positive dir
    distToEdge.x += float(step.x < 0.f) * roFract.x * rayUnitStepSize.x;        //negative dir

    distToEdge.y = float(step.y >= 0.f) * (1 - roFract.y) * rayUnitStepSize.y;
    distToEdge.y += float(step.y < 0.f) * roFract.y * rayUnitStepSize.y;
    
    vec3 radiance = vec3(0.f);
    float prevDist = 0.f;
    float rayIsAlive = 1.0f;
    float gotBlocked = 0.f; //tells other cascades if this hit a wall
    float t = distance(bitmapPC, ro);          //total distance traveled
    float rayMaxDistance = t + RAY_LENGTH;
    float distThroughCell; //for potential volumetrics later.
    
    for(int i = 0; i < MAX_RAY_STEPS; i++) {
        
        if(any(lessThan(cell, ivec2(0))) || any(greaterThanEqual(cell, bitmapSize)))
            break;
        
        //get object data of the current cell
        uint cellData = texelFetch(_bitmapTexture, cell, 0).r;

        //object bools
        float blocks = float((cellData & WALL_MASK) != 0u);
        float emits = float((cellData & EMITTER_MASK) != 0u);
        float transmits = 1.f - blocks;
        
        ////get the emissive material. If it isn't emissive set index to 0. should improve cache hits? TODO: try changing this and compare with tracy!
        //uint matID = (cellData >> 2) * uint(emits);
        //ivec2 matTexelCoord = ivec2(matID % 8, matID / 8);
        //vec4 material = texelFetch(_materialAtlas, matTexelCoord, 0); //0 vector at indx 0
        //vec3 emission = material.rgb * material.a;
        
        vec3 tempEmission = emits * vec3(1f,1f,1f);
        //tempEmission = mix(vec3(1f,0f,0f), tempEmission, insideTexture);
        
        //a wall, a light or reaching max distance kills the ray.

        gotBlocked = max(gotBlocked, blocks);

        
        if(gotBlocked > 0f) {
            break;
        }
        
        //gather radiance
        vec3 newRad = rayIsAlive * tempEmission;// * exp(-airAbsorption * (t / 20)); //beers law for attenuation
        radiance += newRad;
        rayIsAlive *= (1.f - emits) * max(sign(rayMaxDistance - t), 0f);
        
        if(emits > 0f){
            //TODO: make emitters opaque, this rendering system should handle it fine, and because it doesn't something must be wrong.
            gotBlocked = 1;
        }

        //advance DDA
        int xCloser = int(abs(distToEdge.x) < abs(distToEdge.y) +0.001f);
        int yCloser = 1 - xCloser;

        float nextDist = min(distToEdge.x, distToEdge.y);
        distThroughCell = abs(nextDist - prevDist);
        t += distThroughCell;
        prevDist = nextDist;

        cell.x += xCloser * step.x;
        distToEdge.x += xCloser * rayUnitStepSize.x;
        cell.y += yCloser * step.y;
        distToEdge.y += yCloser * rayUnitStepSize.y;
        
        if(t > rayMaxDistance) break;
    }
    //store collected ray data into cascade
    
    
    imageStore(_cascadeTextures, ivec3(id, _CascadeLevel), vec4(radiance, gotBlocked));
}









/*things to try:
        
    * try splitting long high-cascade rays into multiple threads to avoid having an individual thread march 400 steps
    - perhaps 1 group per ray, and then we accumulate the total with shared memory somehow?

    * add transmissives, accumulate attenuation stored in every cell, we should get volumetrics for little extra cost
    
    - to do it travelDist through a cell is needed:
    float travelDist = nextDist - prevDist;  //  can be used to partially affect ray based on volume

*/
 